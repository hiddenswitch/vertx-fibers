plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.hiddenswitch.fibers.instrument' version '1.0.4'
}

repositories {
  mavenCentral()
}

java {
  sourceCompatibility(JavaVersion.VERSION_1_9)
  targetCompatibility(JavaVersion.VERSION_1_9)
  withSourcesJar()
  withJavadocJar()
}

sourceSets {
  examples {
    compileClasspath += main.compileClasspath + main.output
    runtimeClasspath += main.runtimeClasspath + main.output
  }
}

configurations {
  examples.extendsFrom(testImplementation)
}

group = 'com.hiddenswitch'
version = '10.0.3'
def vertxVersion = "4.1.2"

fibers {
  allowBlocking = true
  scanSuspendables = true
  scanSuspendableSupers = true
}

task testJava9(type: Test) {
  javaLauncher.set(javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(9)
  })
}

check.dependsOn testJava9

dependencies {
  api 'com.hiddenswitch:quasar-core:10.0.3'
  compileOnly "io.vertx:vertx-core:$vertxVersion"
  compileOnly 'org.jetbrains:annotations:22.0.0'
  implementation 'org.slf4j:slf4j-api:1.7.32'
  implementation 'com.google.guava:guava:26.0-jre'

  testImplementation "io.vertx:vertx-unit:$vertxVersion"
  testImplementation "io.vertx:vertx-core:$vertxVersion"
  testImplementation(group: 'io.vertx', name: 'vertx-core', version: vertxVersion, classifier: 'tests') {
    exclude group: 'org.hamcrest', module: '*'
    exclude group: 'com.google.errorprone', module: '*'
    exclude group: 'org.checkerframework', module: 'checker-qual'
  }
  testImplementation 'org.hamcrest:hamcrest-core:1.3'
  testImplementation('com.google.guava:guava:30.1.1-jre')
  testImplementation 'junit:junit:4.12'

  examplesImplementation "io.vertx:vertx-web:$vertxVersion"
  examplesImplementation "io.vertx:vertx-pg-client:$vertxVersion"
}

test {
  useJUnit();
  enableAssertions = false
}

task install {
  dependsOn publishToMavenLocal
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'com.hiddenswitch'
      artifactId = 'vertx-fibers'
      from components.java
    }
  }
}
